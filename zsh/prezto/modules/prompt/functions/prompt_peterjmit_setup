#
# A theme using the solarized color palette
#
# Using vcs, completion dots and pwd from sorins theme
#
# Screenshots:
#   http://i.imgur.com/pVjqebl.png
#
# Authors:
#   Sorin Ionescu <sorin.ionescu@gmail.com>
#   Peter Mitchell <pete@peterjmit.com>
#

# Load dependencies.
pmodload 'helper'

function prompt_peterjmit_pwd {
  local pwd="${PWD/#$HOME/~}"

  if [[ "$pwd" == (#m)[/~] ]]; then
    _prompt_peterjmit_pwd="$MATCH"
    unset MATCH
  else
    _prompt_peterjmit_pwd="${${${(@j:/:M)${(@s:/:)pwd}##.#?}:h}%/}/${_prompt_peterjmit_colors[13]}${pwd:t}%f"
  fi
}

function prompt_peterjmit_precmd {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS

  # Format PWD.
  prompt_peterjmit_pwd

  # Get Git repository information.
  if (( $+functions[git-info] )); then
    git-info
  fi
}

function prompt_peterjmit_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)

  # Load required functions.
  autoload -Uz add-zsh-hook

  # Add hook for calling git-info before each command.
  add-zsh-hook precmd prompt_peterjmit_precmd

  # solarized colour palette
  _prompt_peterjmit_colors=(
    "%F{234}" # [1] base03
    "%F{235}" # [2] base02
    "%F{239}" # [3] base01
    "%F{240}" # [4] base00
    "%F{244}" # [5] base0
    "%F{245}" # [6] base1
    "%F{187}" # [7] base2
    "%F{230}" # [8] base3
    "%F{136}" # [9] yellow
    "%F{166}" # [10] orange
    "%F{124}" # [11] red
    "%F{125}" # [12] magenta
    "%F{61}" # [13] violet
    "%F{33}" # [14] blue
    "%F{37}" # [15] cyan
    "%F{64}" # [16] green
  )

  # Set editor-info parameters.
  zstyle ':prezto:module:editor:info:completing' format '%B${_prompt_peterjmit_colors[11]}...%f%b'
  zstyle ':prezto:module:editor:info:keymap:primary' format ' %B${_prompt_peterjmit_colors[11]}❯${_prompt_peterjmit_colors[9]}❯${_prompt_peterjmit_colors[16]}❯%f%b'
  zstyle ':prezto:module:editor:info:keymap:primary:overwrite' format ' ${_prompt_peterjmit_colors[11]}♺%f'
  zstyle ':prezto:module:editor:info:keymap:alternate' format ' %B${_prompt_peterjmit_colors[16]}❮${_prompt_peterjmit_colors[9]}❮${_prompt_peterjmit_colors[11]}❮%f%b'

  # Set git-info parameters.
  zstyle ':prezto:module:git:info' verbose 'yes'
  zstyle ':prezto:module:git:info:action' format ':%%B${_prompt_peterjmit_colors[9]}%s%f%%b'
  zstyle ':prezto:module:git:info:added' format ' %%B${_prompt_peterjmit_colors[16]}✚%f%%b'
  zstyle ':prezto:module:git:info:ahead' format ' %%B${_prompt_peterjmit_colors[9]}⬆%f%%b'
  zstyle ':prezto:module:git:info:behind' format ' %%B${_prompt_peterjmit_colors[9]}⬇%f%%b'
  zstyle ':prezto:module:git:info:branch' format ':${_prompt_peterjmit_colors[9]}%b%f'
  zstyle ':prezto:module:git:info:commit' format ':${_prompt_peterjmit_colors[16]}%.7c%f'
  zstyle ':prezto:module:git:info:deleted' format ' %%B${_prompt_peterjmit_colors[11]}✖%f%%b'
  zstyle ':prezto:module:git:info:modified' format ' %%B${_prompt_peterjmit_colors[14]}✱%f%%b'
  zstyle ':prezto:module:git:info:position' format ':${_prompt_peterjmit_colors[11]}%p%f'
  zstyle ':prezto:module:git:info:renamed' format ' %%B${_prompt_peterjmit_colors[12]}➜%f%%b'
  zstyle ':prezto:module:git:info:stashed' format ' %%B${_prompt_peterjmit_colors[15]}✭%f%%b'
  zstyle ':prezto:module:git:info:unmerged' format ' %%B${_prompt_peterjmit_colors[9]}═%f%%b'
  zstyle ':prezto:module:git:info:untracked' format ' %%B${_prompt_peterjmit_colors[8]}◼%f%%b'
  zstyle ':prezto:module:git:info:keys' format \
    'prompt' '[git$(coalesce "%b" "%p" "%c")%s]' \
    'rprompt' '%A%B%S%a%d%m%r%U%u'

  PROMPT='${_prompt_peterjmit_colors[4]}%m%f » ${_prompt_peterjmit_pwd} ${git_info:+${(e)git_info[prompt]}}% 
→ '

  RPROMPT='${editor_info[overwrite]}%(?:: ${_prompt_peterjmit_colors[11]}⏎%f)${VIM:+" %B${_prompt_peterjmit_colors[16]}V%f%b"}${(e)git_info[rprompt]}'
  SPROMPT='zsh: correct ${_prompt_peterjmit_colors[11]}%R%f to ${_prompt_peterjmit_colors[16]}%r%f [nyae]? '
}

prompt_peterjmit_setup "$@"

